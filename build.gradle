plugins {
  id 'groovy' 
  id 'maven-publish'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

group   = 'org.martinda.gradle.plugins'
version = '1.0.0'

repositories {
  mavenCentral()
}

dependencies {
  compile gradleApi()

  testCompile gradleTestKit()
  testCompile('org.spockframework:spock-core:1.0-groovy-2.4') {
    // Need to exclude else it takes groovy 2.4.1, and it clashes with 2.4.4
    exclude group: 'org.codehaus.groovy', module: 'groovy-all'
  }
  testCompile 'org.codehaus.groovy:groovy:2.4.4'
}

// Gradle 2.9 hack begins (since Gradle 2.6)
// Write the plugin's classpath to a file to share with the tests
task createClasspathManifest {
    def outputDir = file("$buildDir/$name")

    inputs.files sourceSets.main.runtimeClasspath
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()
        file("$outputDir/plugin-classpath.txt").text = sourceSets.main.runtimeClasspath.join("\n")
    }
}

// Add the classpath file to the test runtime classpath
dependencies {
    testRuntime files(createClasspathManifest)
}
// Gradle 2.9 hack ends

